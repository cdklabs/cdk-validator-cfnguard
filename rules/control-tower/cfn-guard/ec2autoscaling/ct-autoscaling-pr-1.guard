# ###################################
##       Rule Specification        ##
#####################################
# 
# Rule Identifier:
#   autoscaling_multiple_az_check
# 
# Description:
#   This control checks whether your Auto Scaling group spans multiple Availability Zones.
# 
# Reports on:
#    AWS::AutoScaling::AutoScalingGroup
# 
# Evaluates:
#   AWS CloudFormation, AWS CloudFormation hook
# 
# Rule Parameters:
#   None
# 
# Scenarios:
# Scenario: 1
#   Given: The input document is an AWS CloudFormation or CloudFormation hook document
#     And: The input document does not contain any Auto Scaling groups
#    Then: SKIP
# Scenario: 2
#   Given: The input document is an AWS CloudFormation or CloudFormation hook document
#     And: The input document contains an Auto Scaling group resource
#     And: 'AvailabilityZones' is not present on the Auto Scaling group resource
#    Then: FAIL
# Scenario: 3
#   Given: The input document is an AWS CloudFormation or CloudFormation hook document
#     And: The input document contains an Auto Scaling group resource
#     And: 'AvailabilityZones' is present on the Auto Scaling group resource
#     And: The number of 'AvailabilityZones' present is less than 2 (< 2) or the number of
#          unique 'AvailabilityZones' provided is less than 2 (< 2)
#    Then: FAIL
# Scenario: 4
#   Given: The input document is an AWS CloudFormation or CloudFormation hook document
#     And: The input document contains an Auto Scaling group resource
#     And: 'AvailabilityZones' is present on the Auto Scaling group resource
#     And: The number of 'AvailabilityZones' present is greater than or equal to 2 (>= 2)
#     And: At least two unique 'AvailabilityZones' have been provided
#    Then: PASS

#
# Constants
#
let AUTOSCALING_GROUP_TYPE = "AWS::AutoScaling::AutoScalingGroup"
let INPUT_DOCUMENT = this

#
# Assignments
#
let autoscaling_groups = Resources.*[ Type == %AUTOSCALING_GROUP_TYPE ]

#
# Primary Rules
#
rule autoscaling_multiple_az_check when is_cfn_template(%INPUT_DOCUMENT)
                                        %autoscaling_groups not empty {
    check(%autoscaling_groups.Properties)
        <<
        [CT.AUTOSCALING.PR.1]: Require an Amazon EC2 Auto Scaling group to have multiple Availability Zones
        [FIX]: Configure Auto Scaling groups with multiple Availability Zones.
        >>
}

rule autoscaling_multiple_az_check when is_cfn_hook(%INPUT_DOCUMENT, %AUTOSCALING_GROUP_TYPE) {
    check(this.%AUTOSCALING_GROUP_TYPE.resourceProperties)
        <<
        [CT.AUTOSCALING.PR.1]: Require an Amazon EC2 Auto Scaling group to have multiple Availability Zones
        [FIX]: Configure Auto Scaling groups with multiple Availability Zones.
        >>
}

#
# Parameterized Rules
#
rule check(autoscaling_group) {
    %autoscaling_group {
       # Scenario 2
       AvailabilityZones exists
       # Scenario 3 and 4
       AvailabilityZones is_list
       AvailabilityZones not empty
       AvailabilityZones[0] exists
       AvailabilityZones[1] exists
       AvailabilityZones[0] not in AvailabilityZones[1]
    }
}

#
# Utility Rules
#
rule is_cfn_template(doc) {
    %doc {
        AWSTemplateFormatVersion exists or
        Resources exists
    }
}

rule is_cfn_hook(doc, RESOURCE_TYPE) {
    %doc.%RESOURCE_TYPE.resourceProperties exists
}
